;license:MIT
;(c) 2023 by 4am
;
!cpu 6502
!to "build/LOADER.SYSTEM#FF2000",plain
*=$2000

; application-specific macros
!macro RWTS_FILENAMES {
side_b
         !byte 6
         !text "SIDE.B"
rwts_filenames
         !word side_b
}

; application-specific addresses
PRODOS_BUFFER             = $B800    ; $400 bytes
PREFIX_BUFFER             = $BC00    ; $40 bytes
AUTO_BUFFER               = $BC00    ; $100 bytes (if non-interactive, persistent)
SAVEGAME                  = $BC00    ; max $100 bytes (if interactive, persistent)
RWTS                      = $BD00    ; max $100 bytes
INPUT                     = $BE00    ; max $100 bytes

         !src  "../common/src/loader.init.a"               ; must be first
                                                           ; exits via Stage1
         !src  "../common/src/loader.rwts.dos.shim.a"
         !src  "../common/src/loader.automate.input.a"
         !src  "../common/src/loader.better.input.a"

Stage1
         ; copy RWTS DOS shim into place
         ; (includes CloseAllFiles routine, so must do this first)
         +INSTALL_RWTS_DOS_SHIM
         ; and set up redirect for DOS calls
         +MAKE_JMP $B7B5, RWTS
         ; and set up RWTS parameter table
         lda   #$01
         sta   $B7EA

         ; set up our custom reset vector
         +STA_16 $3F2, QuitToProDOS
         jsr   ROM_FINALIZE_RESET

         ; 0800..1FFF
         jsr   PRODOS_MLI
         !byte CMD_OPEN
         !word parm_open1
         jsr   PRODOS_MLI
         !byte CMD_READ
         !word parm_read1
         jsr   CloseAllFiles

         ; 4000..8617
         jsr   PRODOS_MLI
         !byte CMD_OPEN
         !word parm_open2
         jsr   PRODOS_MLI
         !byte CMD_READ
         !word parm_read2
         jsr   CloseAllFiles

         ; shorten prompt
         ; TODO move this to OBJ file
         lda   #$3E
         sta   $7359
         lda   #$20
         sta   $735A
         lda   #$00
         sta   $735B

         ; patch game to preserve our custom reset vector
         ; TODO move this to OBJ file
         lda   #$2C
         sta   $6A54
         sta   $6A57

         ; always continue after returning from custom input routine
         ; TODO move this to OBJ file
         lda   #$00
         sta   $6FD1

         ; turn on lowercase display by default on machines that support it
         +HAS_LOWERCASE
         bne   +
         lda   #$00
         sta   $698C
+
         lda   input_filename_start
         beq   @no_auto
         ; if we're using automation, install the AutomateInput hook
         +INSTALL_AUTOMATE_INPUT
         +MAKE_JSR $6FCD, AutomateInput
         ; and bypass title screen / restore game / voice slot
         +MAKE_JMP $6A99, $6ABE
         ; and bypass text intro screen
         lda   #$2C
         sta   $6AC6
         ; and de-randomize with monotonically increasing random values
         lda   #$A9                  ; LDA #$00
         sta   $0F9C
         lda   #$00
         sta   $0F9D
         lda   #$EE                  ; INC $0F9D (value of LDA)
         sta   $0F9E
         lda   #$9D
         sta   $0F9F
         lda   #$0F
         sta   $0FA0
         jmp   @start_game

@no_auto
         ; if we're not using automation, install the BetterInput hook instead
         +INSTALL_BETTER_INPUT
         +MAKE_JSR $6FCD, BetterInput

         ; and copy savegame code into place
         ldx   #(move_savegame_end - move_savegame_start)
-        lda   move_savegame_start-1, x
         sta   SAVEGAME-1, x
         dex
         bne   -

         ; and change save prompt
         ; TODO move this to OBJ file
         ;X=0
-        lda   new_save_prompt, x
         sta   $154F, x
         sta   $750D, x
         beq   +
         inx
         bne   -                     ; always branches
+
         ; and increase save slots
         ; TODO move this to OBJ file
         lda   #$31
         sta   $1541
         sta   $74D9
         lda   #$3A
         sta   $1545
         sta   $74DD

         ; and hook save and restore routines
         +STA_16 $1536, PrintSavedGames
         +STA_16 $74D0, PrintSavedGames
         +STA_16 $775B, OpenSaveFile
         +STA_16 $7799, OpenSaveFile
         +STA_16 $7775, SavePart1
         +STA_16 $7796, SavePart2
         +STA_16 $779C, RestorePart1
         +STA_16 $77AD, RestorePart2

@start_game
         jmp   $0800

new_save_prompt
         !text "GAME NUMBER (1-9)? ",0

parm_open1
         !byte 3
         !word obj_filename1
         !word PRODOS_BUFFER
         !byte 0
parm_read1
         !byte 4
         !byte 1
         !word $0800
         !word $FFFF
         !word 0
obj_filename1
         !byte 8
         !text "OBJ.0800"

parm_open2
         !byte 3
         !word obj_filename2
         !word PRODOS_BUFFER
         !byte 0
parm_read2
         !byte 4
         !byte 1
         !word $4000
         !word $FFFF
         !word 0
obj_filename2
         !byte 8
         !text "OBJ.4000"

move_savegame_start
!pseudopc SAVEGAME {
PrintSavedGames
; in:    none
; out:   all registers preserved
;        exits via $6896
         pha
         tya
         pha
         lda   #$31
         sta   save_slot_ascii
print_loop
         jsr   PRODOS_MLI
         !byte CMD_OPEN
         !word save_open
         bcs   print_slot_next
         jsr   PRODOS_MLI
         !byte CMD_SEEK
         !word print_seek
         bcs   +
         jsr   PRODOS_MLI
         !byte CMD_READ
         !word print_read
+        php
         jsr   CloseAllFiles
         plp
         bcs   print_slot_next
         lda   print_read_actual
         beq   print_slot_next
         lda   save_slot_ascii
         sta   print_slot
         bit   $c051
         lda   #$8D
         jsr   ROM_COUT
         lda   #<print_slot_prefix
         ldy   #>print_slot_prefix
         jsr   PrintPascalString
print_room_id=*+1
         lda   #SELF_MODIFIED_BYTE
         asl
         tay
         lda   $6652, y
         pha
         lda   $6653, y
         tay
         pla
         jsr   PrintPascalString
print_slot_next
         inc   save_slot_ascii
         lda   save_slot_ascii
         cmp   #$3A
         bne   print_loop
print_exit
         lda   #$8D
         jsr   ROM_COUT
         pla
         tay
         pla
         jmp   $6896

PrintPascalString
         sta   print_char_src
         sty   print_char_src+1
         ldy   #0
print_char_loop
print_char_src=*+1
         lda   SELF_MODIFIED_WORD, y
         cpy   #0
         bne   print_char
         sta   print_char_max
         beq   print_char_next       ; always branches (Z still 1 from CPY)
print_char
         ora   #$80
         jsr   ToUpper
         jsr   ROM_COUT
print_char_max=*+1
         cpy   #SELF_MODIFIED_BYTE
         beq   print_cr_before_next_slot
print_char_next
         iny
         bne   print_char_loop       ; always branches
print_cr_before_next_slot
         rts

print_slot_prefix
         !byte 8
         !text "SLOT "
print_slot
         !byte SELF_MODIFIED_BYTE
         !text ": "

print_read
         !byte 4
         !byte 1
         !word print_room_id
         !word 1
print_read_actual
         !word 0
print_seek
         !byte 2
         !byte 1
         !word $0024
         !byte 0

OpenSaveFile
; hooked from $775A (save) and $7798 (restore)
; in:    A = lo-ASCII save slot ($31-$39)
         sta   save_slot_ascii
         jsr   PRODOS_MLI
         !byte CMD_OPEN
         !word save_open
         rts

RestorePart1
; hooked from $779B
         lda   #CMD_READ
         !byte $2C
SavePart1
; hooked from $7774
; in:    $4033 contains first $100 bytes of save data
; out:   returns to caller on success
;        jumps to $782C on error (this is what the original game does,
;        but it's kind of harsh, it prints an error message and hangs)
         lda   #CMD_WRITE
         sta   save_op
         jsr   PRODOS_MLI
save_op  !byte SELF_MODIFIED_BYTE
         !word save_rw
         bcs   save_error
         ldy   save_rw_actual
         bne   save_error
         ldy   save_rw_actual_hi
         dey
         bne   save_error
         rts
save_error
         jsr   CloseAllFiles
         jmp   $782C

RestorePart2
; hooked from $77AC
         jsr   RestorePart1
         jmp   CloseAllFiles

SavePart2
; hooked from $7795
; in:    $4033 contains first $100 bytes of save data
; out:   closes all open ProDOS files
;        returns to caller on success
;        jumps to $782C on error
         jsr   SavePart1
         jmp   CloseAllFiles

save_open
         !byte 3
         !word save_filename
         !word PRODOS_BUFFER
         !byte 0
save_rw
         !byte 4
         !byte 1
save_rw_addr
         !word $4033
save_rw_len
         !word $0100
save_rw_actual
         !byte SELF_MODIFIED_BYTE
save_rw_actual_hi
         !byte SELF_MODIFIED_BYTE

save_filename
         !byte 6
         !text "SAVE."
save_slot_ascii
         !text "0"
}
move_savegame_end
