;license:MIT
;(c) 2024 by 4am
;
; caller must define
; - SAVE_INPUT_BUFFER ($26 bytes)
;

         !src  "../common/src/constants.a"
         !src  "../common/src/macros.a"

; exits with Z=1
!macro INSTALL_BETTER_LINE_INPUT {
         +SHORT_COPY better_input_start, better_input_end, INPUT
}

;------------------------------------------------------------------------------
; BetterLineInput routine
; hook in interactive game mode to provide a better user experience when
; typing
;------------------------------------------------------------------------------
better_input_start
!pseudopc INPUT {
BetterLineInput
line_input_reset
         ldx   #$00
line_input_loop
         jsr   BlockingBetterInput
         cmp   #$88
         bne   line_input_commit
         dex
         bmi   line_input_reset
         dec   CH
         bpl   line_input_loop       ; always branches
line_input_commit
         sta   $200, x
         inx
         ldy   CH
         inc   CH
         cmp   #$8D
         bne   line_input_loop
         jsr   ROM_CLREOL_FROM_Y
         dex
         lda   #$8D
         jmp   ROM_COUT

         !src  "../common/src/shared.betterinput.blocking.a"
         !src  "../common/src/shared.betterinput.a"
}
better_input_end

!if (better_input_end-better_input_start) > $100 {
         !serious "BetterInput routine is too long, ", better_input_end-better_input_start, " bytes"
}
