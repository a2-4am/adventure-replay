;license:MIT
;(c) 2024 by 4am
;
; shared routine to swap critical zero page addresses
;

         !src  "../common/src/constants.a"

first_zp                  = $40
last_zp                   = $4F

SwapZP
         pha
         txa
         pha
         tya
         pha
         ; save/restore chunk of zero page
         ldx   #(last_zp - first_zp)
-        lda   first_zp, x
         ldy   zp_array, x
         sta   zp_array, x
         sty   first_zp, x
         dex
         bpl   -
         ; save/restore $200 (only) which is clobbered by ProDOS
         ; but some games expect buffer at $200 to persist
         ; (e.g. Mystery House)
         lda   $200
         pha
         lda   save_200
         sta   $200
         pla
         sta   save_200
         pla
         tay
         pla
         tax
         pla
         clc
         rts
save_200
         !byte 0
zp_array
         !fill (last_zp - first_zp + 1)
