;license:MIT
;(c) 2023 by 4am
;

!ifndef _MACROS_ {

         !src  "../common/src/constants.a"

!macro LDADDR .addr {
         lda   #<.addr
         ldy   #>.addr
}

!macro STAY .addr {
         sta   .addr
         sty   .addr+1
}

; clobbers A
!macro STA_16 .addr, .value {
         lda   #<.value
         sta   .addr
         lda   #>.value
         sta   .addr+1
}

!macro MAKE_JSR .addr, .dest {
         lda   #$20
         sta   .addr
         +STA_16 .addr+1, .dest
}

; clobbers A
!macro MAKE_JMP .addr, .dest {
         lda   #$4C
         sta   .addr
         +STA_16 .addr+1, .dest
}

; exits with Z=1 if machine can display lowercase
; A clobbered
!macro HAS_LOWERCASE {
         lda   ROM_MACHINEID
         cmp   #$A0                  ; Spectrum ED
         beq   +
         cmp   #$06                  ; Apple //e and later
+
}

; X=0, Z=1 on exit
; A clobbered
; Y preserved
!macro SHORT_COPY .start, .end, .target {
         ldx   #<(.end-.start)
-        lda   .start-1, x
         sta   .target-1, x
         dex
         bne   -
}

; X=0, Z=1 on exit
; A clobbered
; Y preserved
!macro PAGE_COPY .start, .target {
         ldx   #$00
-        lda   .start, x
         sta   .target, x
         dex
         bne   -
}

!macro READ_ENTIRE_FILE .parm_open, .parm_read {
         jsr   PRODOS_MLI
         !byte CMD_OPEN
         !word .parm_open
         jsr   PRODOS_MLI
         !byte CMD_READ
         !word .parm_read
         php
         jsr   CloseAllFiles
         plp
}

_MACROS_=*
}
