;license:MIT
;(c) 2024 by 4am
;
!cpu 6502
!to "build/LOADER.SYSTEM#FF2000",plain
*=$2000

; application-specific macros
!macro RWTS_FILENAMES {
side_b
         !byte 6
         !text "SIDE.B"
rwts_filenames
         !word side_b
}

; application-specific addresses
PRODOS_BUFFER             = $B800    ; $400 bytes
PREFIX_BUFFER             = $BC00    ; $40 bytes
AUTO_BUFFER               = $BC00    ; $100 bytes (if non-interactive, persistent)
SAVEGAME                  = $BC00    ; max $100 bytes (if interactive, persistent)
RWTS                      = $BD00    ; max $100 bytes (persistent)
INPUT                     = $BE00    ; max $100 bytes (persistent)
AUTO_INPUT                = INPUT
AUTO_FILENAME             = $BEBF    ; $41 bytes (if non-interactive, persistent)
SAGA_LOCATION_ARRAY       = $65F3
SAGA_IO_ERROR             = $7811
WRAP_DRAWING              = $0300

         !src  "../common/src/loader.init.a"               ; must be first
                                                           ; exits via Stage1
         !src  "../common/src/loader.rwts.dos.shim.a"
         !src  "../common/src/loader.automateinput.blocking.a"
         !src  "../common/src/loader.betterinput.blocking.a"
         !src  "../common/src/savegame.saga.a"

move_wrap_drawing_start
!pseudopc WRAP_DRAWING {
         txa
         pha
         jsr   EnableAccelerator
         jsr   $1BB6
         jsr   DisableAccelerator
         pla
         tax
         rts
DisableAccelerator
EnableAccelerator=*+3
}
move_wrap_drawing_end
         !src  "../common/src/hw.accel.a"

Stage1
         ; copy RWTS DOS shim into place
         ; (includes CloseAllFiles routine, so must do this first)
         +INSTALL_RWTS_DOS_SHIM
         ; and set up redirect for DOS calls
         +MAKE_JMP $B7B5, RWTS
         ; and set up RWTS parameter table
         lda   #$01
         sta   $B7EA

         ; set up our custom reset vector
         +STA_16 $3F2, QuitToProDOS
         jsr   ROM_FINALIZE_RESET

         ; 0800..1FFF
         +READ_ENTIRE_FILE parm_open1, parm_read1

         ; 4000..8617
         +READ_ENTIRE_FILE parm_open2, parm_read2

; these changes have been made in the OBJ files directly
!if 0 {
         ; shorten prompt
         lda   #$3E
         sta   $7351
         lda   #$20
         sta   $7352
         lda   #$00
         sta   $7353

         ; patch game to preserve our custom reset vector
         lda   #$2C
         sta   $6A5F
         sta   $6A62

         ; always continue after returning from custom input routine
         lda   #$00
         sta   $6FDC
}

         ; copy accelerated drawing wrapper into place
         +SHORT_COPY move_wrap_drawing_start, move_wrap_drawing_end, WRAP_DRAWING

         ; build EnableAccelerator and DisableAccelerator functions
         ; and copy them into place
         jsr   BuildAcceleratorFunction
         +STAY @accel+1
         dex
@accel   lda   SELF_MODIFIED_WORD, x
         sta   DisableAccelerator, x
         dex
         bpl   @accel

         ; hook accelerated drawing routine
         +STA_16 $163D, WRAP_DRAWING

         ; turn on lowercase display by default on machines that support it
         +HAS_LOWERCASE
         bne   +
         lda   #$00
         sta   $698C
+
         lda   input_filename_start
         beq   @no_auto
         ; if we're using automation, install the AutomateInput hook
         +INSTALL_AUTOMATE_INPUT
         +MAKE_JSR $6FD8, AutomateInput
         ; and bypass title screen / restore game / voice slot
         +MAKE_JMP $6AA4, $6AC9
         ; and bypass text intro screen
         lda   #$2C
         sta   $6AD1
         ; and de-randomize with monotonically increasing random values
         lda   #$A9                  ; LDA #$00
         sta   $0F9C
         lda   #$00
         sta   $0F9D
         lda   #$EE                  ; INC $0F9D (value of LDA)
         sta   $0F9E
         lda   #$9D
         sta   $0F9F
         lda   #$0F
         sta   $0FA0
         jmp   @start_game

@no_auto
         ; if we're not using automation, install the BetterInput hook instead
         +INSTALL_BETTER_INPUT
         +MAKE_JSR $6FD8, BlockingBetterInput

         ; copy savegame code into place
         +INSTALL_SAGA_SAVEGAME

; these changes have been made in the OBJ files directly
!if 0 {
         ; and change save prompt
         ;X=0
-        lda   new_save_prompt, x
         sta   $154F, x
         sta   $7505, x
         beq   +
         inx
         bne   -                     ; always branches
+
         ; and increase save slots
         lda   #$31
         sta   $1541
         sta   $74D1
         lda   #$3A
         sta   $1545
         sta   $74D5
}

         ; and hook save and restore routines
         +STA_16 $1536, PrintSavedGames
         +STA_16 $74C8, PrintSavedGames
         +STA_16 $7740, OpenSaveFile
         +STA_16 $777E, OpenSaveFile
         +STA_16 $775A, SavePart1
         +STA_16 $777B, SavePart2
         +STA_16 $7781, RestorePart1
         +STA_16 $7792, RestorePart2

@start_game
         jmp   $0800

; these changes have been made in the OBJ files directly
!if 0 {
new_save_prompt
         !text "GAME NUMBER (1-9)? ",0
}

parm_open1
         !byte 3
         !word obj_filename1
         !word PRODOS_BUFFER
         !byte 0
parm_read1
         !byte 4
         !byte 1
         !word $0800
         !word $FFFF
         !word 0
obj_filename1
         !byte 8
         !text "OBJ.0800"

parm_open2
         !byte 3
         !word obj_filename2
         !word PRODOS_BUFFER
         !byte 0
parm_read2
         !byte 4
         !byte 1
         !word $4000
         !word $FFFF
         !word 0
obj_filename2
         !byte 8
         !text "OBJ.4000"
