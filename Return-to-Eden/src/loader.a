;license:BSD-3-Clause
;extended open/read/write binary file in ProDOS filesystem, with random access
;copyright (c) Peter Ferrie 2013-2021
;assemble using ACME

ver_02 = 1

!if ver_02 = 1 {
  !cpu 6502
} else { ;ver_02 = 0
  !cpu 65c02
} ;ver_02

!to "build/LOADER.SYSTEM#FF2000",plain
*=$2000

STAGE2                    = $B600    ; $100 bytes

         ; ProRWTS user-defined options - no code
         !src  "src/prorwts.options.a"

         ; ProRWTS user-defined driver load address - no code
         !src  "src/prorwts.address.a"

         ; ProRWTS constants (some depend on options) - no code
         !src  "../common/src/prorwts.constants.a"

         !src  "../common/src/constants.a"         ; no code
         !src  "../common/src/macros.a"            ; no code

;
; .SYSTEM code starts here
;
         jmp   init_machine          ; magic jump
         !byte $EE,$EE               ; magic bytes
         !byte $41                   ; length of following buffer
input_filename_start
         !fill $41                   ; may contain filename passed in by caller
input_filename_end

;
; Reset machine to a known state
;
init_machine
         jsr   ROM_IN0
         jsr   ROM_PR0

init
         !src  "../common/src/prorwts.stage1.a"

         +SHORT_COPY input_filename_start, input_filename_end, AUTO_FILENAME
         +PAGE_COPY foo, STAGE2

         lda   #cmdread
         sta   reqcmd
         lda   #>file1
         sta   namhi
         lda   #<file1
         sta   namlo
         ldx   #$20
         ldy   #$00
         tya
         jmp   STAGE2

foo !pseudopc STAGE2 {
xx       sta   $2000,y
         iny
         bne   xx
         inc   xx+2
         dex
         bne   xx
         lda   $c057
         lda   $c052
         lda   $c050

-
sz       ldy   #0
         lda   sztbl,y
         sta   sizehi
         jsr   hddopendir
         lda   $c083
         inc   sz+1
         inc   f1_e-1
         lda   f1_e-1
         eor   #'6'
         bne   -
         ldx   #6
         tay
zz       lda   $b000,y
         sta   $200,y
         iny
         bne   zz
         inc   sz+1
         inc   zz+2
         inc   zz+5
         dex
         bne   zz
         lda   bauto
         beq   +
         ldx   #2
-        lda   hook1,x
         sta   $400,x
         lda   hook2,x
         sta   $878,x
         dex
         bpl   -
         lda   #>bauto
         sta   namhi
         lda   #<bauto
         sta   namlo
         lda   #2
         sta   sizehi
         jsr   hddopendir
+
         jmp   $900

file1 !byte f1_e-f1_b
f1_b	!text	"GAME.1"
f1_e

sztbl	!byte	>(9728+511)&$fe, >(3072+511)&$fe, >(5109+511)&$fe, >(26880+511)&$fe, >(1536+511)&$fe
hook1   !byte $20,<keyhook,>keyhook
hook2   !byte $20,<drawhook,>drawhook

bauto
}
AUTO_FILENAME

unrelochdd
!pseudopc reloc {
         !src  "../common/src/prorwts.reloc.a"

keyhook  lda   #0
patch    nop
ptr      lda   $f900
         beq   +++
         cmp   #$0d
         bne   +
         jsr   togglekeyhook
         lda   #$0d
+        inc   ptr+1
         bne   ++
         inc   ptr+2
         pha
         lda   ptr+2
         cmp   #$fb
         bcc   +
         lda   #$f9
         sta   ptr+2
         sta   ldrhi
         lda   #2
         sta   sizehi
         jsr   hddrdfile
+        pla
++       ora   #$80
+++      rts

drawhook jsr   $d1eb
togglekeyhook
         lda   patch
         eor   #$60 xor $ea
         sta   patch
         rts
hddcodeend
hdddataend
} ;reloc

         !src  "../common/src/prorwts.finalize.a"
