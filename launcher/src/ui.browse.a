;license:MIT
;(c) 2024 by 4am
;

kBrowseKeys                          ; must keep in sync with kBrowseKeyHandlers arrays
         !byte $8D                   ; Return
         !byte $8B                   ; up arrow
         !byte $88                   ; left arrow
         !byte $8A                   ; down arrow
         !byte $95                   ; right arrow
         !byte $9B                   ; Esc
         !byte $00                   ; terminates key list, corresponds to default handler

kBrowseKeyHandlers
         !word BrowseEventReturn
         !word BrowseEventUp
         !word BrowseEventUp
         !word BrowseEventDown
         !word BrowseEventDown
         !word BrowseEventEsc
         !word BrowseEventDefault

DrawBrowse
         ldx   BrowseIndex
         jsr   LoadTitleOffscreen
         lda   #$14
         sta   startLine
         lda   #$01
         sta   lineCount
         ; calculate starting point within txtBrowse
         +LDADDR txtBrowse
         ldx   BrowseIndex
-        dex
         beq   @done
         clc
         adc   #$28
         bcc   -
         iny
         bne   -
@done    jsr   DrawTextOffscreen
         inc   startLine             ; -> $15
         inc   lineCount             ; -> $01 because DrawTextOffscreen decrements it to 0
         jsr   ROM_INVERSE
         jsr   DrawMoreTextOffscreen
         jsr   ROM_NORMAL
         inc   startLine             ; -> $16
         lda   #$02
         sta   lineCount
         jsr   DrawMoreTextOffscreen
         jmp   ShowOtherPage

BrowseEventReturn
         lda   BrowseIndex
         sta   GameID
         lda   #kScreenGameDetails
         jmp   GoForward

BrowseEventUp
         ldx   BrowseIndex
         dex
         bne   +
         ldx   GameCount
+        stx   BrowseIndex
         sec
         ldx   #kContinue
         rts

BrowseEventDown
         ldx   BrowseIndex
         cpx   GameCount
         bne   +
         ldx   #$00
+        inx
         stx   BrowseIndex
         sec
         ldx   #kContinue
         rts

BrowseEventEsc
         jmp   GoBack

BrowseEventDefault
         clc
         ldx   #kContinue
         rts
